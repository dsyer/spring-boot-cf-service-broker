buildscript {
    repositories {
        maven { url "http://repo.spring.io/libs-snapshot" }
        mavenLocal()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.0.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'maven-publish'

version = "2.3.3"
group = "org.cloudfoundry"
description = "Spring boot project for creating cloudfoundry service brokers"

ext {
    springBootVersion = '1.1.4.RELEASE'
    hibernateValidatorVersion = '5.1.0.Final'
    jsonPathVersion = '0.9.1'
    baseName = "spring-boot-cf-service-broker"
    baseNameTests = baseName + "-tests"
}

jar {
    baseName = baseName
    version =  version
}

task testsJar(type: Jar) {
  from sourceSets.test.output
  baseName = baseNameTests
  version =  version
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/libs-snapshot" }
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
    compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
    compile("org.hibernate:hibernate-validator:${hibernateValidatorVersion}")
    testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testCompile("com.jayway.jsonpath:json-path:${jsonPathVersion}")
}

publishing {
  publications {
    jar(MavenPublication) {
      groupId group
      artifactId baseName
      version version
      from components.java
    }
    test(MavenPublication) {
      groupId group
      artifactId baseNameTests
      version version
      artifact testsJar
    }
    mavenJava(MavenPublication) {
      from components.java  
      artifact sourceJar {
        classifier "sources"
      }
    }
  }
  repositories {
    maven {
      credentials {
        username "${artifactory_user?:null}"
        password "${artifactory_password?:null}"
      }
      url "${artifactory_contextUrl?:null}/libs-release-local"
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
